apply plugin: 'com.android.library'

com.android.ddmlib.DdmPreferences.setTimeOut(600000)

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 2
        versionName "4.0.2"

        buildConfigField "String", "STAGING_SERVER_URL", staging_server_url
        buildConfigField "String", "INSTANCE_NAME", instance_name
        buildConfigField "String", "API_KEY", api_key
        buildConfigField "String", "API_KEY_USERS", api_key_users
        buildConfigField "String", "FACEBOOK_TOKEN", facebook_token
        buildConfigField "String", "GOOGLE_TOKEN", google_token
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
        }
    }

    adbOptions {
        timeOutInMs 600000
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task(makeJar) << {
    // Empty. We'll add dependencies for this task below
}

// Generate jar creation tasks for all build variants
android.libraryVariants.all { variant ->
    String taskName = "makeJar${variant.name.capitalize()}"

    // Create a jar by extracting it from the assembled .aar
    // This ensures that products distributed via .aar and .jar exactly the same bits
    task(taskName, type: Copy) {
        String archiveName = "${project.name}-${variant.name}"
        String outputDir = "${buildDir.getPath()}/outputs"

        dependsOn "assemble${variant.name.capitalize()}"
        from(zipTree("${outputDir}/aar/${archiveName}.aar"))
        into("${outputDir}/jar/")
        include('classes.jar')
        rename('classes.jar', "${archiveName}-${variant.mergedFlavor.versionName}.jar")
    }

    makeJar.dependsOn tasks[taskName]
}